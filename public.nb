(* MAPPING & COMMA-BASIS UTILITIES *)

(*

dimensionality[matrix]

Determines whether the given matrix is a mapping or comma-basis,
then returns the dimensionality of the temperament it represents.

Examples:

In    meantoneMapping = {{5, 8, 12}, {7, 11, 16}};
      dimensionality[meantoneMapping]

Out   3

In    meantoneCommaBasis = {{4}, {-4}, {1}};
      dimensionality[meantoneCommaBasis]

Out   3

*)
dimensionality[a_] := Max[rowCount[a], colCount[a]];

(*

rank[matrix]

Determines whether the given matrix is a mapping or comma-basis,
then returns the rank of the temperament it represents.

If the matrix is square, this errors, because there's no way to tell.

Examples:

In    meantoneMapping = {{5, 8, 12}, {7, 11, 16}};
      rank[meantoneMapping]

Out   2

In    meantoneCommaBasis = {{4}, {-4}, {1}};
      rank[meantoneCommaBasis]

Out   2

*)
rank[a_] := If[
  isSquare[a],
  Error,
  If[
    isMapping[a],
    MatrixRank[a],
    dimensionality[a] - nullity[a]
  ]
];

(*

nullity[matrix]

Determines whether the given matrix is a mapping or comma-basis,
then returns the nullity of the temperament it represents.

If the matrix is square, this errors, because there's no way to tell.

Examples:

In    meantoneMapping = {{5, 8, 12}, {7, 11, 16}};
      nullity[meantoneMapping]

Out   1

In    meantoneCommaBasis = {{4}, {-4}, {1}};
      nullity[meantoneCommaBasis]

Out   1

*)
nullity[a_] := If[
  isSquare[a],
  Error,
  If[
    isCommaBasis[a],
    MatrixRank[Transpose[a]],
    dimensionality[a] - rank[a]
  ]
];


(* MATRIX FORMS & DEFACTORING *)

(*

canonicalForm[matrix]

Determines whether the given matrix is a mapping or comma-basis,
then returns it in canonical form (defactored, then put into
Hermite Normal Form).

If the matrix is square and not equal to its own transpose,
you must provide either "commaBasis" or "mapping" as a second argument.

Examples:

In    meantoneMapping = {{5, 8, 12}, {7, 11, 16}};
      canonicalForm[meantoneMapping]

Out   {{1, 0, -4}, {0, 1, 4}}

In    meantoneCommaBasis = {{-4}, {4}, {-1}};
      canonicalForm[meantoneCommaBasis]

Out   {{4}, {-4}, {1}};

*)
canonicalForm[a_, type_ : Null] := If[
  allZeros[a],
  a,
  If[
    SameQ[ type, "commaBasis"] || isCommaBasis[a],
    antiTranspose[hnf[columnHermiteDefactor[antiTranspose[a]]]],
    If[
      SameQ[ type, "mapping"]  || isMapping[a] || Transpose[a] == a,
      hnf[columnHermiteDefactor[a]],
      Error
    ]
  ]
];


(* DUAL *)

(*

nullSpaceBasis[mapping(, dimensionality)]

Given a mapping, returns its dual comma-basis: a basis for its null-space.
The comma-basis is returned in canonical form.

Examples:

In    meantoneMapping = {{5, 8, 12}, {7, 11, 16}};
      nullSpaceBasis[meantoneMapping]

Out   {{4}, {-4}, {1}}

*)
nullSpaceBasis[m_] := Module[{c},
  c = canonicalForm[noncanonicalNullSpaceBasis[m]];

  If[c == {}, Transpose[{Table[0, dimensionality[m]]}], c]
];

(*

antiNullSpaceBasis[commaBasis(, dimensionality)]

Given a comma-basis, returns its dual mapping.
This is the opposite of finding the null-space-basis,
and therefore can be considered the anti-null-space-basis.
The mapping is returned in canonical form.

Examples:

In    meantoneCommaBasis = {{4}, {-4}, {1}};
      antiNullSpaceBasis[meantoneCommaBasis]

Out   {{1, 0, -4}, {0, 1, 4}}

*)
antiNullSpaceBasis[c_] := Module[{m},
  m = canonicalForm[antiTranspose[noncanonicalNullSpaceBasis[antiTranspose[c]]]];

  If[m == {}, {Table[0, dimensionality[c]]}, m]
];
(*

dual[matrix(, dimensionality)]

Determines whether the given matrix is a mapping or comma-basis, then returns its dual.

If the matrix is square and not equal to its own transpose,
you must provide either "commaBasis" or "mapping" as a second argument.

Examples:

In    meantoneMapping = {{1, 0, -4}, {0, 1, 4}};
      dual[meantoneMapping]

Out   {{4}, {-4}, {1}}

*)
dual[a_, type_ : Null] := If[
  SameQ[ type, "commaBasis"]  || isCommaBasis[a],
  antiNullSpaceBasis[a],
  If[
    SameQ[ type, "mapping"]  || isMapping[a]|| Transpose[a] == a,
    nullSpaceBasis[a],
    Error
  ]
];


(* MEET AND JOIN *)
